plugins {
	id 'org.springframework.boot' version '2.7.0'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id "io.freefair.lombok" version "6.5.1"
	id "com.diffplug.spotless" version "6.12.0"
    id 'com.google.cloud.tools.jib' version '3.3.2'
}

group = 'bungae.thunder'
version = '0.0.1'
sourceCompatibility = '11'

repositories {
	mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springdoc:springdoc-openapi-ui:1.7.0'
    testImplementation 'junit:junit:4.13.1'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.mockito:mockito-core:4.8.0'
}

tasks.named('test') {
    useJUnitPlatform()
}

task installGitLocalHook(type: Copy) {
    from 'scripts/pre-commit'
    into '.git/hooks'
    fileMode 0775
}

build.dependsOn installGitLocalHook

/**
 * ref)
 * https://nesoy.github.io/articles/2020-04/Spotless
 * https://github.com/diffplug/spotless/tree/main/plugin-gradle
 */
spotless {
    java {
        // 사용하지 않는 import 제거
        removeUnusedImports()
        // 공백 제거
        trimTrailingWhitespace()
        // 끝부분 NewLine 처리
        endWithNewline()

        /**
         * import 순서도 정의가 가능하다.
         */
        importOrder 'java', 'javax', 'org', 'com', 'com.diffplug', ''

        /**
         * Google Java Format을 쉽게 적용할 수 있다.
         */
        googleJavaFormat().aosp()
    }

    /**
     * 특정 파일에 대해 적용할 수 있다.
     */
    format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'

        trimTrailingWhitespace()
        endWithNewline()
    }
}

jib {
    from {
        image = "openjdk:11-jre-slim"
        platforms {
            platform {
                architecture = "arm64"
                os = "linux"
            }
            platform {
                architecture = "amd64"
                os = "linux"
            }
        }
    }

    to {
        image = "haakjunlee/cakey-backend"
        tags = ["0.0.1", "latest"]
    }

    container {
        jvmFlags = ["-Xms128m", "-Xmx128m"]
    }
}
